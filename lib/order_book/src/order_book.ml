open! Core
open Jsip_types
open Async_log_kernel.Ppx_log_syntax

type t =
  { symbol : Symbol.t
  ; mutable bids : Order.t list
  ; mutable asks : Order.t list
  }
[@@deriving sexp_of]

let create symbol = { symbol; bids = []; asks = [] }
let symbol t = t.symbol

let side_list t side =
  match (side : Side.t) with Buy -> t.bids | Sell -> t.asks
;;

let set_side_list t side orders =
  match (side : Side.t) with
  | Buy -> t.bids <- orders
  | Sell -> t.asks <- orders
;;

let add t order =
  let side = Order.side order in
  set_side_list t side (order :: side_list t side)
;;

let remove' t order_id =
  let remove_from t side order_id =
    let orders = side_list t side in
    match
      List.partition_tf orders ~f:(fun o ->
        Order_id.equal (Order.order_id o) order_id)
    with
    | [], _ -> None
    | [ found ], rest ->
      set_side_list t side rest;
      Some found
    | matches, _ ->
      [%log.info
        "BUG: More than one order matching order_id found when removing"
          (order_id : Order_id.t)
          (matches : Order.t list)
          (t.symbol : Symbol.t)
          (side : Side.t)];
      None
  in
  match remove_from t Buy order_id with
  | Some _ as result -> result
  | None -> remove_from t Sell order_id
;;

let remove t order_id = ignore (remove' t order_id : Order.t option)

let find t order_id =
  let find_in side =
    List.find (side_list t side) ~f:(fun o ->
      Order_id.equal (Order.order_id o) order_id)
  in
  match find_in Buy with Some _ as result -> result | None -> find_in Sell
;;

(* NOTE: This walks the list front-to-back and returns the *first* tradable
   order, not the best-priced one. Orders are in reverse insertion order
   (newest first), so this matches against whatever was most recently added,
   regardless of price. See test_matching_engine.ml for a test that
   demonstrates why this is wrong. *)
let find_match t incoming =
  let incoming_side = Order.side incoming in
  let opposite_side = Side.flip incoming_side in
  let resting_orders = side_list t opposite_side in
  let marketable_orders =
    List.filter resting_orders ~f:(fun resting ->
      Price.is_marketable
        incoming_side
        ~price:(Order.price incoming)
        ~resting_price:(Order.price resting))
  in
  (* To compare orders, first compare by price and take the more aggressive
     priced order. If equally priced, take the better order time wise. *)
  List.reduce marketable_orders ~f:(fun best resting ->
    if Price.is_more_aggressive
         opposite_side
         ~price:(Order.price resting)
         ~than:(Order.price best)
    then resting
    else if Price.equal (Order.price resting) (Order.price best)
    then
      if Order_id.compare (Order.order_id resting) (Order.order_id best) < 0
      then resting
      else best
    else best)
;;

let orders_on_side t side = side_list t side
let is_empty t = List.is_empty t.bids && List.is_empty t.asks
let count t side = List.length (side_list t side)

let best_price t side =
  match side_list t side with
  | [] -> None
  | first :: rest ->
    Some
      (List.fold rest ~init:(Order.price first) ~f:(fun best order ->
         let price = Order.price order in
         if Price.is_more_aggressive side ~price ~than:best
         then price
         else best))
;;

let best_level t side : Level.t option =
  match best_price t side with
  | None -> None
  | Some price ->
    let total_size =
      List.fold (side_list t side) ~init:Size.zero ~f:(fun acc order ->
        if Price.equal (Order.price order) price
        then Size.( + ) acc (Order.remaining_size order)
        else acc)
    in
    Some { price; size = total_size }
;;

let best_bid_offer t : Bbo.t =
  { bid = best_level t Buy; ask = best_level t Sell }
;;

let snapshot_side t (side : Side.t) =
  let compare o1 o2 =
    let p1 = Order.price o1 in
    let p2 = Order.price o2 in
    if Price.equal p1 p2
    then Order_id.compare (Order.order_id o1) (Order.order_id o2)
    else if Price.is_more_aggressive side ~price:p1 ~than:p2
    then -1
    else 1
  in
  orders_on_side t side |> List.sort ~compare |> List.map ~f:Level.of_order
;;

(* let snapshot_side t (side : Side.t) = let compare = match side with | Buy
   -> Comparable.reverse Level.compare | Sell -> Level.compare in
   orders_on_side t side |> List.map ~f:Level.of_order |> List.sort ~compare
   ;; *)
let snapshot t =
  { Book.symbol = symbol t
  ; bids = snapshot_side t Buy
  ; asks = snapshot_side t Sell
  ; bbo = best_bid_offer t
  }
;;

module For_testing = struct
  let remove = remove'
end
